// button1 and button2 are flags set when interrupts are triggered by rising edges on pins 2 and 3 respectively
volatile boolean button1 = false, button2 = false;

void setup ()
{
  // Start serail communication at 9600 baud
  Serial.begin(9600);
  // Call extIntSetup function which sets up the required registers for external interrupt opperation
  extIntSetup(); 
  // Enable external interrupts
  extIntStart(); 
}

void loop ()
{
  // Check if either of button1 or button2 has been set true, and if so print which button to serial
  if (button1 == true)
  {
    Serial.println("Button 1 Pressed!");
    button1 = false;
  }
  if (button2 == true) 
  {
    Serial.println("Button 2 Pressed!");
    button2 = false;
  }
}

// External interrupt 0 (pin 2) ISR
ISR(INT0_vect)
{
  button1 = true;
}

// External interrupt 1 (pin 3) ISR
ISR(INT1_vect)
{
  button2 = true;
}

void extIntSetup()
{
  // Disble global interrupts
  cli();
  // External Interrupt 0 generated by rising edge on INT0
  EICRA |= (1 << ISC01);
  EICRA |= (1 << ISC00);

  // External Interrupt 1 generated by rising edge on INT1
  EICRA |= (1 << ISC11);
  EICRA |= (1 << ISC10);
  // Enable global interrupts
  sei();
}

void extIntStart()
{
  // Disble global interrupts
  cli();
  // Enable External Interrupt 0 and 1
  EIMSK |= (1 << INT0);
  EIMSK |= (1 << INT1);
  // Enable global interrupts
  sei();
}

